<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html>
<head>
<title>Vývoj her pro herní konzoli NES: scrolling</title>
<meta name="Author" content="Pavel Tisnovsky" />
<meta name="Generator" content="vim" />
<meta http-equiv="content-type" content="text/html; charset=utf-8" />
<style type="text/css">
         body {color:#000000; background:#ffffff;}
         h1  {font-family: arial, helvetica, sans-serif; color:#ffffff; background-color:#c00000; text-align:center; padding-left:1em}
         h2  {font-family: arial, helvetica, sans-serif; color:#ffffff; background-color:#0000c0; padding-left:1em; text-align:left}
         h3  {font-family: arial, helvetica, sans-serif; color:#000000; background-color:#c0c0c0; padding-left:1em; text-align:left}
         h4  {font-family: arial, helvetica, sans-serif; color:#000000; background-color:#e0e0e0; padding-left:1em; text-align:left}
         a   {font-family: arial, helvetica, sans-serif;}
         li  {font-family: arial, helvetica, sans-serif; color:#000000; text-align:justify; width:450px;}
         ol  {font-family: arial, helvetica, sans-serif; color:#000000; text-align:justify; width:450px;}
         ul  {font-family: arial, helvetica, sans-serif; color:#000000; text-align:justify; width:450px;}
         p   {font-family: arial, helvetica, sans-serif; color:#000000; text-align:justify;}
         pre {background:#e0e0e0}
</style>
</head>

<body>

<h1>Vývoj her pro herní konzoli NES: scrolling</h1>

<h3>Pavel Tišnovský</h3>

<p></p>

<h1>Úvodník</h1>

<p>Poslední vlastností grafického čipu PPU v osmibitové herní konzoli NES je podpora pro scrolling celého pozadí. Tato technika je použita v mnoha hrách, například v Super Mario Bros či ve hře Castlevania. Dnes si ukážeme, jak lze scrolling pozadí realizovat v assembleru.</p>



<h2>Obsah</h2>

<p><a href="#k01">1. Vývoj her pro herní konzoli NES: scrolling</a></p>
<p><a href="#k02">2. Vliv atributu <i>mirroring</i> v&nbsp;hlavičce ROM</a></p>
<p><a href="#k03">3. Oprava obslužné rutiny RESET: povolení NMI až na samotném konci rutiny</a></p>
<p><a href="#k04">*** 4. </a></p>
<p><a href="#k05">*** 5. </a></p>
<p><a href="#k06">*** 6. </a></p>
<p><a href="#k07">7. Úplný zdrojový kód prvního demonstračního příkladu</a></p>
<p><a href="#k08">8. Ukázka pozadí vykresleného prvním demonstračním příkladem</a></p>
<p><a href="#k09">9. Realizace scrollingu pozadí v&nbsp;NMI rutině</a></p>
<p><a href="#k10">10. Změna offsetů pro scrolling s&nbsp;využitím ovladače</a></p>
<p><a href="#k11">11. Úplný zdrojový kód druhého demonstračního příkladu</a></p>
<p><a href="#k12">12. Ukázky posunutého pozadí</a></p>
<p><a href="#k13">13. Zobrazení aktuální hodnoty offsetů přímo na pozadí</a></p>
<p><a href="#k14">14. Získání a uložení tvarů dvou hexadecimálních cifer</a></p>
<p><a href="#k15">*** 15. Integrace nového kódu do obslužné rutiny NMI</a></p>
<p><a href="#k16">16. Úplný zdrojový kód třetího demonstračního příkladu</a></p>
<p><a href="#k17">*** 17. Ukázky posunutého pozadí se zobrazením offsetů</a></p>
<p><a href="#k18">*** 18. Od assembleru k&nbsp;jazyku C</a></p>
<p><a href="#k19">19. Repositář s&nbsp;demonstračními příklady</a></p>
<p><a href="#k20">20. Odkazy na Internetu</a></p>



<p><a name="k01"></a></p>
<h2 id="k01">1. Vývoj her pro herní konzoli NES: scrolling</h2>

<p>V&nbsp;předchozích částech tohoto seriálu jsme si postupně ukázali, jak se
na herní konzoli NES tvoří hudba a zvuky, dále jsme si popsali i odzkoušeli
způsob vykreslení spritů, včetně změny jejich vlastností (pozice, priorita vůči
pozadí atd.). A minule jsme si řekli, jak se vykresluje pozadí herní scény.
Statické pozadí se ovšem používá jen v&nbsp;některých hrách; z&nbsp;těch
známějších například ve hře Donkey Kong a samozřejmě i v&nbsp;Tetrisu. Ovšem
mnohé další hry dokážou pozadím scrollovat, tj.&nbsp;pohybovat jím
v&nbsp;horizontálním a/nebo i ve vertikálním směru. Příkladem jsou různé
střílečky, ale i například populární hra Castlevania.</p>

*** image ***
<p><i>Obrázek 1: Úvodní obrazovka hry Castlevania I na NESu.</i></p>

<p>Nejjednodušší je &bdquo;skokový&ldquo; posun pozadí o šestnáct pixelů
jakýmkoli směrem, protože v&nbsp;tomto případě stačí jen přepsat hodnoty
v&nbsp;tabulce jmen i tabulce atributů (blokový přenos cca 1kB dat). Ovšem již
posun o osm pixelů může kolidovat s&nbsp;tabulkami atributů, protože atributy
jsou uloženy vždy pro čtyři sousední dlaždice. Nás ovšem bude zajímat plynulý
posun pozadí o jednotlivé pixely, což je hardwarem herní konzole NES
taktéž umožněno.</p>

*** image ***
<p><i>Obrázek 2: Pěkně navržená grafika hry Castlevania I (obrázek má
v&nbsp;tomto případě pouze 15 barev).</i></p>

*** image ***
<p><i>Obrázek 3: Castlevania I na NES.</i></p>

*** image ***
<p><i>Obrázek 4: Castlevania I na NES.</i></p>

*** image ***
<p><i>Obrázek 5: Castlevania I na NES.</i></p>



<p><a name="k02"></a></p>
<h2 id="k02">2. Vliv atributu <i>mirroring</i> v&nbsp;hlavičce ROM</h2>

<p>Na to, jak bude scrolling probíhat, má vliv bitový atribut <i>mirroring</i>
ukládaný do hlavičky ROM s&nbsp;hrou:</p>

<pre>
<i>; Mirroring (0 = horizontal, 1 = vertical)</i>
mirroring = 1
&nbsp;
.segment "HEADER"
        .byte $4e, $45, $53, $1a
        .byte prg_npage
        .byte chr_npage
        .byte ((mapper &amp; $0f) &lt;&lt; 4) | (mirroring &amp; 1)
        .byte mapper &amp; $f0
</pre>

<p>Změnou tohoto atributu lze ovlivnit, jak se budou při scrollingu chovat ty
části scény, které leží &bdquo;mimo původní obrazovku&ldquo;, přesněji řečeno
mimo hodnoty uložené ve jmenné tabulce. Při nastavení vertikálního zrcadlení
bude <a
href="https://www.nesdev.org/wiki/File:Vertical_mirroring_diagram.png">situace
vypadat následovně</a>, zatímco u horizontálního zrcadlení <a
href="https://www.nesdev.org/wiki/File:Horizontal_mirroring_diagram.png">bude
vypadat takto</a>. Prozatím si však vystačíme pouze s&nbsp;jednou jmennou
tabulkou s&nbsp;tím, že při scrollingu postupně budeme sledovat hodnoty
v&nbsp;tabulkách obsahujících jen prázdné dlaždice.</p>



<p><a name="k03"></a></p>
<h2 id="k03">3. Oprava obslužné rutiny RESET: povolení NMI až na samotném konci rutiny</h2>

<p>Na rozdíl od předchozích demonstračních příkladů, v&nbsp;nichž v&nbsp;rutině
RESET neprobíhaly dlouhotrvající operace, nyní musíme vykreslit rozsáhlejší
pozadí. Z&nbsp;tohoto důvodu je nutné zajistit, aby se NMI (tedy přerušení
zavolané před vykreslením dalšího snímku) zavolalo až na samotném konci rutiny
RESET, ideálně až v&nbsp;nekonečné herní smyčce. To lze zařídit snadno &ndash;
přesunem kódu pro povolení přerušení až před samotnou smyčku:</p>

<pre>
<i>; Obslužná rutina pro RESET</i>
&nbsp;
.proc <strong>reset</strong>
        ...
        ...
        ...
        lda #%00001110        <i>; povolení zobrazení pozadí, nikoli ovšem spritů</i>
        sta PPUMASK
&nbsp;
        cli                   <i>; vynulování bitu I - povolení přerušení</i>
        lda #%10010000      
        sta PPUCTRL           <i>; při každém VBLANK se vyvolá NMI (důležité!)</i>
&nbsp;
        <i>; vlastní herní smyčka je prozatím prázdná</i>
game_loop:
        jmp game_loop           <i>; nekonečná smyčka (později rozšíříme)</i>
.endproc
</pre>

<p><div class="rs-tip-major">Poznámka: opomenutí této opravy vás může stát
hodně času při ladění &bdquo;náhodných chyb&ldquo;; ostatně přesně toto se
stalo autorovi tohoto článku :-)</div></p>



<p><a name="k04"></a></p>
<h2 id="k04">4. </h2>

<pre>
<i>; tabulka jmen</i>
nametabledata:
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24

    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24

    .byte $24,$1b,$18,$18,$1d,$24,$25,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
</pre>

<pre>
<i>; tabulka atributů</i>
attributedata:
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111 <i>; žluto-hnědá paleta</i>
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; zelená paleta</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
    .byte %10101010, %10101010, %10101010, %10111111, %11111111, %10101010, %11111111, %10101010
    .byte %01010101, %10101010, %01010101, %11101111, %11111111, %01010101, %11111111, %01010101
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; jen obloha - průhledné pixely</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
</pre>



<p><a name="k05"></a></p>
<h2 id="k05">5. </h2>

<pre>
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>
&nbsp;
.proc <strong>nmi</strong>
        lda #0
        sta SCROLL        <i>; zákaz scrollingu</i>
        sta SCROLL
        lda #$02          <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti               <i>; návrat z přerušení</i>
.endproc
</pre>



<p><a name="k07"></a></p>
<h2 id="k07">7. Úplný zdrojový kód prvního demonstračního příkladu</h2>

<p>Úplný zdrojový kód dnešního prvního demonstračního příkladu (v&nbsp;pořadí
již dvacátého osmého příkladu pro NES) je dostupný na adrese <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example28.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example28.asm</a>.
Pro překlad a slinkování tohoto příkladu je zapotřebí i <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/Makefile">Makefile</a>
a příkaz <strong>make example28.nes</strong>:</p>

<pre>
<i>; ---------------------------------------------------------------------</i>
<i>; Kostra programu pro herní konzoli NES</i>
<i>; Nastavení barvové palety, zvýšení intenzity barvy</i>
<i>; Setup PPU přes makro</i>
<i>; Definice pozadí a zobrazení pozadí</i>
<i>;</i>
<i>; Založeno na příkladu https://github.com/depp/ctnes/tree/master/nesdev/01</i>
<i>; Taktéž založeno na https://nerdy-nights.nes.science/#main_tutorial-3</i>
<i>; Viz též článek na https://www.moria.us/blog/2018/03/nes-development</i>
<i>; Audio https://raw.githubusercontent.com/iliak/nes/master/doc/apu_ref.txt</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Jména řídicích registrů použitých v kódu</i>
PPUCTRL         = $2000
PPUMASK         = $2001
PPUSTATUS       = $2002
SCROLL          = $2005
PPUADDR         = $2006
PPUDATA         = $2007
DMC_FREQ        = $4010
OAM_DMA         = $4014
&nbsp;
<i>; Další důležité adresy</i>
PALETTE         = $3f00
NAME_TABLE_0    = $2000
ATTRIB_TABLE_0  = $23c0
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice maker</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.macro <strong>setup_cpu</strong>
        <i>; nastavení stavu CPU</i>
        sei                     <i>; zákaz přerušení</i>
        cld                     <i>; vypnutí dekadického režimu (není podporován)</i>
&nbsp;
        ldx #$ff
        txs                     <i>; vrchol zásobníku nastaven na 0xff (první stránka)</i>
.endmacro
&nbsp;
.macro <strong>wait_for_frame</strong>
:       bit PPUSTATUS            <i>; test obsahu registru PPUSTATUS </i>
        bpl :-                   <i>; skok, pokud je příznak N nulový</i>
.endmacro
&nbsp;
.macro <strong>clear_ram</strong>
        lda #$00                <i>; vynulování registru A</i>
:       sta $000, x             <i>; vynulování X-tého bajtu v nulté stránce</i>
        sta $100, x
        sta $200, x
        sta $300, x
        sta $400, x
        sta $500, x
        sta $600, x
        sta $700, x             <i>; vynulování X-tého bajtu v sedmé stránce</i>
        inx                     <i>; přechod na další bajt</i>
        bne :-                  <i>; po přetečení 0xff -> 0x00 konec smyčky</i>
.endmacro
&nbsp;
.macro <strong>ppu_data_palette_address</strong>
        lda PPUSTATUS   <i>; reset záchytného registru</i>
        lda #&gt;PALETTE   <i>; nastavení adresy pro barvovou paletu $3f00</i>
        sta PPUADDR
        lda #&lt;PALETTE   <i>; nižší bajt adresy</i>
        sta PPUADDR
.endmacro
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice hlavičky obrazu ROM</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Size of PRG in units of 16 KiB.</i>
prg_npage = 2
&nbsp;
<i>; Size of CHR in units of 8 KiB.</i>
chr_npage = 1
&nbsp;
<i>; INES mapper number.</i>
mapper = 0
&nbsp;
<i>; Mirroring (0 = horizontal, 1 = vertical)</i>
mirroring = 1
&nbsp;
.segment "HEADER"
        .byte $4e, $45, $53, $1a
        .byte prg_npage
        .byte chr_npage
        .byte ((mapper &amp; $0f) &lt;&lt; 4) | (mirroring &amp; 1)
        .byte mapper &amp; $f0
&nbsp;
.segment "ZEROPAGE"
.segment "STARTUP"
.segment "CODE"
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Blok paměti s definicí dlaždic 8x8 pixelů</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "CHR0a"
.segment "CHR0b"
&nbsp;
&nbsp;
.code
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Programový kód rutin pro NMI, RESET a IRQ volaných automaticky CPU</i>
<i>;</i>
<i>; viz též https://www.pagetable.com/?p=410</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>
&nbsp;
.proc <strong>nmi</strong>
        lda #0
        sta SCROLL        <i>; zákaz scrollingu</i>
        sta SCROLL
        lda #$02          <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti               <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro IRQ (maskovatelné přerušení)</i>
&nbsp;
.proc <strong>irq</strong>
        rti                     <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro RESET</i>
&nbsp;
.proc <strong>reset</strong>
        <i>; nastavení stavu CPU</i>
        setup_cpu
&nbsp;
        <i>; nastavení řídicích registrů</i>
        ldx #$40
        stx $4017               <i>; zákaz IRQ z APU</i>
&nbsp;
        ldx #$00
        stx PPUCTRL             <i>; nastavení PPUCTRL = 0 (NMI)</i>
        stx PPUMASK             <i>; nastavení PPUMASK = 0</i>
        stx DMC_FREQ            <i>; zákaz DMC IRQ</i>
&nbsp;
        ldx #$40
        stx $4017               <i>; interrupt inhibit bit</i>
&nbsp;
        <i>; čekání na vnitřní inicializaci PPU (dva snímky)</i>
        wait_for_frame
        wait_for_frame
&nbsp;
        <i>; vymazání obsahu RAM</i>
        clear_ram
&nbsp;
        <i>; čekání na další snímek</i>
        wait_for_frame
&nbsp;
        <i>; nastavení tabulek jmen</i>
        jsr clear_nametables  <i>; zavolání subrutiny </i>
&nbsp;
        <i>; nastavení barvové palety</i>
        jsr load_palette      <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení tabulky jmen</i>
        jsr load_nametable    <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení atributů</i>
        jsr load_attributes   <i>; zavolání subrutiny</i>
&nbsp;
        lda #%00001110        <i>; povolení zobrazení pozadí, nikoli ovšem spritů</i>
        sta PPUMASK
&nbsp;
        cli                   <i>; vynulování bitu I - povolení přerušení</i>
        lda #%10010000      
        sta PPUCTRL           <i>; při každém VBLANK se vyvolá NMI (důležité!)</i>
&nbsp;
        <i>; vlastní herní smyčka je prozatím prázdná</i>
game_loop:
        jmp game_loop           <i>; nekonečná smyčka (později rozšíříme)</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; vynulování tabulek jmen</i>
.proc <strong>clear_nametables</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
        ldx #$08           <i>; počitadlo stránek (8)</i>
        ldy #$00           <i>; X a Y tvoří 16bitový čítač</i>
        lda #$24           <i>; dlaždice číslo $24 představuje oblohu</i>
:
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        dey                <i>; snížení hodnoty počitadla</i>
        bne :-             <i>; skok dokud se nezaplní celá stránka</i>
        dex                <i>; snížení hodnoty počitadla stránek</i>
        bne :-             <i>; skok dokud se nezaplní 8 stránek</i>
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
<i>; nastavení barvové palety</i>
.proc <strong>load_palette</strong>
        ppu_data_palette_address
&nbsp;
        <i>; $3f00-$3f0f - paleta pozadí</i>
        <i>; $3f10-$3f1f - paleta spritů</i>
&nbsp;
        ldx #$00        <i>; vynulovat počitadlo a offset</i>
&nbsp;
:
        lda palette, x  <i>; načíst bajt s offsetem</i>
        sta PPUDATA     <i>; zápis barvy do PPU</i>
        inx             <i>; zvýšit počitadlo/offset</i>
        cpx #32         <i>; limit počtu barev</i>
        bne :-          <i>; opakovat smyčku 32x</i>
&nbsp;
        rts             <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení tabulky jmen</i>
.proc <strong>load_nametable</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+256,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+512,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení atributů</i>
.proc <strong>load_attributes</strong>
        lda PPUSTATUS        <i>; reset záchytného registru</i>
        lda #&gt;ATTRIB_TABLE_0 <i>; horní bajt adresy $23c0</i>
        sta PPUADDR
        lda #&lt;ATTRIB_TABLE_0 <i>; spodní bajt adresy $23c0</i>
        sta PPUADDR
&nbsp;
        ldx #0               <i>; počitadlo 64 bajtů</i>
&nbsp;
:
        lda attributedata,X  <i>; načtení čtyř atributů</i>
        sta PPUDATA          <i>; zápis indexu</i>
        inx                  <i>; snížení hodnoty počitadla</i>
        cpx #64
        bne :-               <i>; opakování smyčky</i>
&nbsp;
        rts                  <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; samotná barvová paleta</i>
palette:
    .byte $22, $29, $1a, $0F, $22, $36, $17, $0F, $22, $30, $21, $0F, $22, $27, $17, $0F  <i>; barvy pozadí</i>
    .byte $22, $16, $27, $18, $22, $1A, $30, $27, $22, $16, $30, $27, $22, $0F, $36, $17  <i>; barvy spritů</i>
&nbsp;
&nbsp;
<i>; tabulka jmen</i>
nametabledata:
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$1b,$18,$18,$1d,$24,$25,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
&nbsp;
<i>; tabulka atributů</i>
attributedata:
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111 <i>; žluto-hnědá paleta</i>
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; zelená paleta</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
    .byte %10101010, %10101010, %10101010, %10111111, %11111111, %10101010, %11111111, %10101010
    .byte %01010101, %10101010, %01010101, %11101111, %11111111, %01010101, %11111111, %01010101
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; jen obloha - průhledné pixely</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Tabulka vektorů CPU</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "VECTORS"
.addr nmi
.addr reset
.addr irq
&nbsp;
&nbsp;
&nbsp;
.segment "CHARS"
    .incbin "mario.chr"
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Finito</i>
<i>; ---------------------------------------------------------------------</i>
</pre>



<p><a name="k08"></a></p>
<h2 id="k08">8. Ukázka pozadí vykresleného prvním demonstračním příkladem</h2>

<p>Po překladu dnešního prvního demonstračního příkladu příkazem:</p>

<pre>
$ <strong>make example28.nes</strong>
</pre>

<p>Můžeme tento příklad spustit v&nbsp;emulátoru herní konzole NES, například
<a href="https://www.emulator-zone.com/nes/nestopia">v&nbsp;Nestopii</a>:</p>

<pre>
$ <strong>nestopia example28.nes</strong>
</pre>

<p>Výsledkem by měla být tato obrazovka se statickým pozadím:</p>

*** image ***
<p><i>Obrázek 6: Výsledek běhu dnešního prvního demonstračního
příkladu.</i></p>



<p><a name="k09"></a></p>
<h2 id="k09">9. Realizace scrollingu pozadí v&nbsp;NMI rutině</h2>

<p>Podívejme se nyní na způsob realizace scrollingu celého pozadí. Nejprve si
nadeklarujeme pojmenované buňky, které budou obsahovat informaci o tom, o kolik
pixelů se má pozadí posunout, a to jak v&nbsp;horizontálním směru, tak i ve
směru vertikálním. Tyto buňky můžeme alokovat například na začátek druhé
stránky paměti (dostupné CPU, nikoli PPU) nebo (pro kratší a rychlejší kód) do
nulté stránky paměti. Buňky si pojmenujeme, například následujícím
způsobem:</p>

<pre>
<i>; Adresy globálních proměnných</i>
XSCROLL         = $0203       <i>; adresa buňky paměti pro scrolling ve směru x-ové osy</i>
YSCROLL         = $0200       <i>; adresa buňky paměti pro scrolling ve směru y-ové osy</i>
</pre>

<p>Nyní bude v&nbsp;assembleru instrukce:</p>

<pre>
        lda XSCROLL
</pre>

<p>znamenat &bdquo;načti do akumulátoru obsah paměťové buňky $0203&ldquo;</p>

<p>a instrukce:</p>

<pre>
        sta XSCROLL
</pre>

<p>bude naopak znamenat &bdquo;ulož obsah akumulátoru do paměťové buňky
$0203&ldquo;.</p>

<p>Prakticky na samotném konci obslužné rutiny NMI je nutné hodnoty obou
offsetů uložit do řídicího registru <strong>SCROLL</strong>, a to přesně
v&nbsp;uvedeném pořadí:</p>

<pre>
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>
&nbsp;
.proc <strong>nmi</strong>
        ...
        ...
        ...
        lda XSCROLL           <i>; nastavení scrollingu</i>
        sta SCROLL
        lda YSCROLL
        sta SCROLL
&nbsp;
        lda #$02              <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti                   <i>; návrat z přerušení</i>
.endproc
</pre>

<p><div class="rs-tip-major">Poznámka: samotný registr <strong>SCROLL</strong>
je uložen na této adrese:</div></p>

<pre>
SCROLL          = $2005
</pre>



<p><a name="k10"></a></p>
<h2 id="k10">10. Změna offsetů pro scrolling s&nbsp;využitím ovladače</h2>

<p>Samotná modifikace offsetů, tedy hodnot uložených na adresách
<strong>XSCROLL</strong> a <strong>YSCROLL</strong>, je vlastně triviální,
protože jsme podobný kód viděli při posunu spritů. Budeme testovat stav všech
tlačítek ovladače a pokud se bude detekovat stisk tlačítek nahoru, dolů,
doprava či doleva, provedeme pouhou změnu hodnoty buňky
<strong>XSCROLL</strong> nebo <strong>YSCROLL</strong>. V&nbsp;případě
mikroprocesoru MOS 6502 lze tuto operaci provést velmi snadno jedinou instrukcí
<strong>INC</strong> nebo <strong>DEC</strong>, tj.&nbsp;bez nutnosti načítání
hodnoty do akumulátoru, provedení příslušné operace a uložení výsledku zpět do
operační paměti.</p>

<p>Nejprve je nutné realizovat načtení stavu všech osmi tlačítek na ovladači do
záchytného registru:</p>

<pre>
        lda #$01
        sta JOYPAD1           <i>; načtení stavu všech osmi tlačítek do záchytného registru</i>
</pre>

<p>Následně se stav všech tlačítek přenese do posuvného registru:</p>

<pre>
        lda #$00
        sta JOYPAD1           <i>; začátek načítání jednotlivých bitů se stavy tlačítek v tomto pořadí:</i>
                              <i>; </i>
                              <i>; 1) A                      </i>
                              <i>; 2) B                      </i>
                              <i>; 3) Select                 </i>
                              <i>; 4) Start                  </i>
                              <i>; 5) Up                     </i>
                              <i>; 6) Down                   </i>
                              <i>; 7) Left                   </i>
                              <i>; 8) Right</i>
</pre>

<p>Tlačítka A, B, Select a Start budeme ignorovat, ovšem z&nbsp;posuvného
registru je musíme vyčíst:</p>

<pre>
        read_button           <i>; detekce stisku tlačítka A</i>
        read_button           <i>; detekce stisku tlačítka B</i>
        read_button           <i>; detekce stisku tlačítka Select</i>
        read_button           <i>; detekce stisku tlačítka Start</i>
</pre>

<p>Pouze směrová tlačítka ovlivní scrolling. Tyto operace jsou přímočaré:</p>

<pre>
        read_button           <i>; detekce stisku tlačítka Up</i>
        beq up_not_pressed    <i>; není stisknuto? => skok</i>
        inc YSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
up_not_pressed:
        read_button           <i>; detekce stisku tlačítka Down</i>
        beq down_not_pressed  <i>; není stisknuto? => skok</i>
        dec YSCROLL           <i>; snížení offsetu</i>
&nbsp;
down_not_pressed:
        read_button           <i>; detekce stisku tlačítka Left</i>
        beq left_not_pressed  <i>; není stisknuto? => skok</i>
        inc XSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
left_not_pressed:
        read_button           <i>; detekce stisku tlačítka Right</i>
        beq right_not_pressed <i>; není stisknuto? => skok</i>
        dec XSCROLL           <i>; snížení offsetu</i>
&nbsp;
right_not_pressed:
</pre>



<p><a name="k11"></a></p>
<h2 id="k11">11. Úplný zdrojový kód druhého demonstračního příkladu</h2>

<p>Úplný zdrojový kód dnešního druhého demonstračního příkladu (v&nbsp;pořadí
již dvacátého devátého příkladu pro NES) je dostupný na adrese <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example29.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example29.asm</a>.
Pro překlad a slinkování tohoto příkladu je zapotřebí i <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/Makefile">Makefile</a>
a příkaz <strong>make example29.nes</strong>:</p>

<pre>
<i>; ---------------------------------------------------------------------</i>
<i>; Kostra programu pro herní konzoli NES</i>
<i>; Nastavení barvové palety, zvýšení intenzity barvy</i>
<i>; Setup PPU přes makro</i>
<i>; Definice pozadí a zobrazení pozadí</i>
<i>; Scrolling pozadí</i>
<i>;</i>
<i>; Založeno na příkladu https://github.com/depp/ctnes/tree/master/nesdev/01</i>
<i>; Taktéž založeno na https://nerdy-nights.nes.science/#main_tutorial-3</i>
<i>; Viz též článek na https://www.moria.us/blog/2018/03/nes-development</i>
<i>; Audio https://raw.githubusercontent.com/iliak/nes/master/doc/apu_ref.txt</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Jména řídicích registrů použitých v kódu</i>
PPUCTRL         = $2000
PPUMASK         = $2001
PPUSTATUS       = $2002
SCROLL          = $2005
PPUADDR         = $2006
PPUDATA         = $2007
DMC_FREQ        = $4010
OAM_DMA         = $4014
&nbsp;
<i>; Další důležité adresy</i>
PALETTE         = $3f00
NAME_TABLE_0    = $2000
ATTRIB_TABLE_0  = $23c0
&nbsp;
<i>; Ovladače</i>
JOYPAD1         = $4016
JOYPAD2         = $4017
&nbsp;
<i>; Adresy globálních proměnných</i>
XSCROLL         = $0203       <i>; adresa buňky paměti pro scrolling ve směru x-ové osy</i>
YSCROLL         = $0200       <i>; adresa buňky paměti pro scrolling ve směru y-ové osy</i>
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice maker</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.macro <strong>setup_cpu</strong>
        <i>; nastavení stavu CPU</i>
        sei                     <i>; zákaz přerušení</i>
        cld                     <i>; vypnutí dekadického režimu (není podporován)</i>
&nbsp;
        ldx #$ff
        txs                     <i>; vrchol zásobníku nastaven na 0xff (první stránka)</i>
.endmacro
&nbsp;
.macro <strong>wait_for_frame</strong>
:       bit PPUSTATUS            <i>; test obsahu registru PPUSTATUS </i>
        bpl :-                   <i>; skok, pokud je příznak N nulový</i>
.endmacro
&nbsp;
.macro <strong>clear_ram</strong>
        lda #$00                <i>; vynulování registru A</i>
:       sta $000, x             <i>; vynulování X-tého bajtu v nulté stránce</i>
        sta $100, x
        sta $200, x
        sta $300, x
        sta $400, x
        sta $500, x
        sta $600, x
        sta $700, x             <i>; vynulování X-tého bajtu v sedmé stránce</i>
        inx                     <i>; přechod na další bajt</i>
        bne :-                  <i>; po přetečení 0xff -> 0x00 konec smyčky</i>
.endmacro
&nbsp;
.macro <strong>ppu_data_palette_address</strong>
        lda PPUSTATUS   <i>; reset záchytného registru</i>
        lda #&gt;PALETTE   <i>; nastavení adresy pro barvovou paletu $3f00</i>
        sta PPUADDR
        lda #&lt;PALETTE   <i>; nižší bajt adresy</i>
        sta PPUADDR
.endmacro
&nbsp;
.macro <strong>read_button</strong>
        lda JOYPAD1        <i>; stav tlačítka</i>
        and #%00000001     <i>; maskovat všechny bity kromě prvního</i>
.endmacro
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice hlavičky obrazu ROM</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Size of PRG in units of 16 KiB.</i>
prg_npage = 2
&nbsp;
<i>; Size of CHR in units of 8 KiB.</i>
chr_npage = 1
&nbsp;
<i>; INES mapper number.</i>
mapper = 0
&nbsp;
<i>; Mirroring (0 = horizontal, 1 = vertical)</i>
mirroring = 1
&nbsp;
.segment "HEADER"
        .byte $4e, $45, $53, $1a
        .byte prg_npage
        .byte chr_npage
        .byte ((mapper &amp; $0f) &lt;&lt; 4) | (mirroring &amp; 1)
        .byte mapper &amp; $f0
&nbsp;
.segment "ZEROPAGE"
.segment "STARTUP"
.segment "CODE"
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Blok paměti s definicí dlaždic 8x8 pixelů</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "CHR0a"
.segment "CHR0b"
&nbsp;
&nbsp;
.code
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Programový kód rutin pro NMI, RESET a IRQ volaných automaticky CPU</i>
<i>;</i>
<i>; viz též https://www.pagetable.com/?p=410</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>
&nbsp;
.proc <strong>nmi</strong>
        lda #$01
        sta JOYPAD1           <i>; načtení stavu všech osmi tlačítek do záchytného registru</i>
        lda #$00
        sta JOYPAD1           <i>; začátek načítání jednotlivých bitů se stavy tlačítek v tomto pořadí:</i>
                              <i>; </i>
                              <i>; 1) A                      </i>
                              <i>; 2) B                      </i>
                              <i>; 3) Select                 </i>
                              <i>; 4) Start                  </i>
                              <i>; 5) Up                     </i>
                              <i>; 6) Down                   </i>
                              <i>; 7) Left                   </i>
                              <i>; 8) Right</i>
&nbsp;
        read_button           <i>; detekce stisku tlačítka A</i>
        read_button           <i>; detekce stisku tlačítka B</i>
        read_button           <i>; detekce stisku tlačítka Select</i>
        read_button           <i>; detekce stisku tlačítka Start</i>
&nbsp;
        read_button           <i>; detekce stisku tlačítka Up</i>
        beq up_not_pressed    <i>; není stisknuto? => skok</i>
        inc YSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
up_not_pressed:
        read_button           <i>; detekce stisku tlačítka Down</i>
        beq down_not_pressed  <i>; není stisknuto? => skok</i>
        dec YSCROLL           <i>; snížení offsetu</i>
&nbsp;
down_not_pressed:
        read_button           <i>; detekce stisku tlačítka Left</i>
        beq left_not_pressed  <i>; není stisknuto? => skok</i>
        inc XSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
left_not_pressed:
        read_button           <i>; detekce stisku tlačítka Right</i>
        beq right_not_pressed <i>; není stisknuto? => skok</i>
        dec XSCROLL           <i>; snížení offsetu</i>
&nbsp;
right_not_pressed:
        lda XSCROLL           <i>; nastavení scrollingu</i>
        sta SCROLL
        lda YSCROLL
        sta SCROLL
&nbsp;
        lda #$02              <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti                   <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro IRQ (maskovatelné přerušení)</i>
&nbsp;
.proc <strong>irq</strong>
        rti                   <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro RESET</i>
&nbsp;
.proc <strong>reset</strong>
        <i>; nastavení stavu CPU</i>
        setup_cpu
&nbsp;
        <i>; nastavení řídicích registrů</i>
        ldx #$40
        stx $4017               <i>; zákaz IRQ z APU</i>
&nbsp;
        ldx #$00
        stx PPUCTRL             <i>; nastavení PPUCTRL = 0 (NMI)</i>
        stx PPUMASK             <i>; nastavení PPUMASK = 0</i>
        stx DMC_FREQ            <i>; zákaz DMC IRQ</i>
&nbsp;
        ldx #$40
        stx $4017               <i>; interrupt inhibit bit</i>
&nbsp;
        <i>; čekání na vnitřní inicializaci PPU (dva snímky)</i>
        wait_for_frame
        wait_for_frame
&nbsp;
        <i>; vymazání obsahu RAM</i>
        clear_ram
&nbsp;
        <i>; čekání na další snímek</i>
        wait_for_frame
&nbsp;
        <i>; nastavení tabulek jmen</i>
        jsr clear_nametables  <i>; zavolání subrutiny </i>
&nbsp;
        <i>; nastavení barvové palety</i>
        jsr load_palette      <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení tabulky jmen</i>
        jsr load_nametable    <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení atributů</i>
        jsr load_attributes   <i>; zavolání subrutiny</i>
&nbsp;
        lda #%00001110        <i>; povolení zobrazení pozadí, nikoli ovšem spritů</i>
        sta PPUMASK
&nbsp;
        lda #0                <i>; vynulovat počitadlo spritů</i>
        sta XSCROLL
        sta YSCROLL
&nbsp;
        cli                   <i>; vynulování bitu I - povolení přerušení</i>
        lda #%10010000      
        sta PPUCTRL           <i>; při každém VBLANK se vyvolá NMI (důležité!)</i>
&nbsp;
        <i>; vlastní herní smyčka je prozatím prázdná</i>
game_loop:
        jmp game_loop           <i>; nekonečná smyčka (později rozšíříme)</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; vynulování tabulek jmen</i>
.proc <strong>clear_nametables</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
        ldx #$08           <i>; počitadlo stránek (8)</i>
        ldy #$00           <i>; X a Y tvoří 16bitový čítač</i>
        lda #$24           <i>; dlaždice číslo $24 představuje oblohu</i>
:
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        dey                <i>; snížení hodnoty počitadla</i>
        bne :-             <i>; skok dokud se nezaplní celá stránka</i>
        dex                <i>; snížení hodnoty počitadla stránek</i>
        bne :-             <i>; skok dokud se nezaplní 8 stránek</i>
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
<i>; nastavení barvové palety</i>
.proc <strong>load_palette</strong>
        ppu_data_palette_address
&nbsp;
        <i>; $3f00-$3f0f - paleta pozadí</i>
        <i>; $3f10-$3f1f - paleta spritů</i>
&nbsp;
        ldx #$00        <i>; vynulovat počitadlo a offset</i>
&nbsp;
:
        lda palette, x  <i>; načíst bajt s offsetem</i>
        sta PPUDATA     <i>; zápis barvy do PPU</i>
        inx             <i>; zvýšit počitadlo/offset</i>
        cpx #32         <i>; limit počtu barev</i>
        bne :-          <i>; opakovat smyčku 32x</i>
&nbsp;
        rts             <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení tabulky jmen</i>
.proc <strong>load_nametable</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+256,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+512,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení atributů</i>
.proc <strong>load_attributes</strong>
        lda PPUSTATUS        <i>; reset záchytného registru</i>
        lda #&gt;ATTRIB_TABLE_0 <i>; horní bajt adresy $23c0</i>
        sta PPUADDR
        lda #&lt;ATTRIB_TABLE_0 <i>; spodní bajt adresy $23c0</i>
        sta PPUADDR
&nbsp;
        ldx #0               <i>; počitadlo 64 bajtů</i>
&nbsp;
:
        lda attributedata,X  <i>; načtení čtyř atributů</i>
        sta PPUDATA          <i>; zápis indexu</i>
        inx                  <i>; snížení hodnoty počitadla</i>
        cpx #64
        bne :-               <i>; opakování smyčky</i>
&nbsp;
        rts                  <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; samotná barvová paleta</i>
palette:
    .byte $22, $29, $1a, $0F, $22, $36, $17, $0F, $22, $30, $21, $0F, $22, $27, $17, $0F  <i>; barvy pozadí</i>
    .byte $22, $16, $27, $18, $22, $1A, $30, $27, $22, $16, $30, $27, $22, $0F, $36, $17  <i>; barvy spritů</i>
&nbsp;
&nbsp;
<i>; tabulka jmen</i>
nametabledata:
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$1b,$18,$18,$1d,$24,$25,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
&nbsp;
<i>; tabulka atributů</i>
attributedata:
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111 <i>; žluto-hnědá paleta</i>
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; zelená paleta</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
    .byte %10101010, %10101010, %10101010, %10111111, %11111111, %10101010, %11111111, %10101010
    .byte %01010101, %10101010, %01010101, %11101111, %11111111, %01010101, %11111111, %01010101
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; jen obloha - průhledné pixely</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Tabulka vektorů CPU</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "VECTORS"
.addr nmi
.addr reset
.addr irq
&nbsp;
&nbsp;
&nbsp;
.segment "CHARS"
    .incbin "mario.chr"
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Finito</i>
<i>; ---------------------------------------------------------------------</i>
</pre>



<p><a name="k12"></a></p>
<h2 id="k12">12. Ukázky posunutého pozadí</h2>

<p>Podívejme se nyní na to, jak mohou vypadat scény vykreslené dnešním druhým
demonstračním příkladem. Povšimněte si, že pro některé posuny (offsety) se
vykreslí pouze modrá obrazovka, tj.&nbsp;vlastně druhá obrazovka uložená
v&nbsp;paměti PPU:</p>

*** image ***
<p><i>Obrázek 7: Posun pozadí doleva.</i></p>

*** image ***
<p><i>Obrázek 8: Posun pozadí doprava.</i></p>

*** image ***
<p><i>Obrázek 9: Posun pozadí nahoru.</i></p>

*** image ***
<p><i>Obrázek 10: Posun pozadí dolů.</i></p>



<p><a name="k13"></a></p>
<h2 id="k13">13. Zobrazení aktuální hodnoty offsetů přímo na pozadí</h2>

<p>Herní konzole NES nám neumožňuje snadnou realizaci funkce typu
<strong>printf</strong> nebo alespoň <strong>puts</strong>, ovšem při práci
s&nbsp;objekty, jejichž hodnoty se mohou měnit, je vhodné nějakým způsobem
zajistit zobrazení příslušných hodnot přímo na pozadí. To se týká mj.&nbsp;i
offsetů pro posun pozadí, což jsou dvě hodnoty v&nbsp;rozsahu od 0 do 255.
Každou z&nbsp;těchto hodnot zobrazíme jako dvojici hexadecimálních číslic na
plochu pozadí. Proč hexadecimálních? Je tomu tak z&nbsp;toho důvodu, že převod
hodnoty 0..255 na dvojici hexadecimálních číslic je v&nbsp;assembleru snadný,
resp.&nbsp;mnohem snadnější, než pokusy o dělení deseti atd.</p>

<p>Jakmile vypočteme jednu z&nbsp;cifer 0-F, můžeme ji zobrazit ve formě
dlaždice s&nbsp;indexem taktéž 0-F, a to díky tomu, že prvních deset dlaždic
&bdquo;náhodou&ldquo; obsahuje právě číslice následované znaky abecedy:</p>

<img src="https://i.iinfo.cz/images/130/nes-devel-3.png" class="image-576977" alt="&#160;" title="Autor: tisnik, podle licence: &lt;a href=&quot;https://creativecommons.org/publicdomain/mark/1.0/deed.en&quot;&gt;Public Domain&lt;/a&gt;" width="524" height="617" />
<p><i>Obrázek 11: Dlaždice, které lze použít pro výpis zpráv na pozadí.</i></p>

<p>Subrutina pro zobrazení obou offsetů může vypadat takto. Povšimněte si volby
horního a spodního bajtu tak, aby byly offsety zobrazeny zhruba uprostřed:</p>

<pre>
<i>; zobrazení offsetů</i>
.proc <strong>display_scroll_offsets</strong>
        lda PPUSTATUS         <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0+2  <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0+10 <i>; spodní bajt adresy</i>
        sta PPUADDR
&nbsp;
        <strong>draw_two_digits XSCROLL</strong>
&nbsp;       
        lda #$24              <i>; mezera (tvar oblohy)</i>
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
&nbsp;
        <strong>draw_two_digits YSCROLL</strong>
&nbsp;
        rts
.endproc
</pre>

<p><div class="rs-tip-major">Poznámka: mezi dvojicí číslic je uložena dlaždice
obsahující jen barvu oblohy, tedy vlastně mezera.</div></p>



<p><a name="k14"></a></p>
<h2 id="k14">14. Získání a uložení tvarů dvou hexadecimálních cifer</h2>

<p>V&nbsp;předchozí subrutině bylo voláno makro
<strong>draw_two_digits</strong>, které pro hodnotu uloženou na zvolené adrese
<strong>address</strong> získá nejprve horní číslici a uloží ji do registru
PPUDATA a posléze totéž provede s&nbsp;dolní číslicí. Bitové (logické) posuny
lze provádět pouze o jediný bit, takže je kód nepatrně delší:</p>

<pre>
<i>; zobrazení dvou hexadecimálních cifer na pozadí</i>
.macro <strong>draw_two_digits address</strong>
        lda address           <i>; přečíst offset pro scrolling</i>
        lsr a                 <i>; získání horního nibblu</i>
        lsr a
        lsr a
        lsr a
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
&nbsp;
        lda address           <i>; opět přečíst offset pro scrolling</i>
        and #$0f              <i>; získání horního nibblu</i>
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
.endmacro
</pre>



<p><a name="k15"></a></p>
<h2 id="k15">15. Integrace nového kódu do obslužné rutiny NMI</h2>


<pre>
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>

.proc <strong>nmi</strong>
        jsr display_scroll_offsets

        lda #$01
        sta JOYPAD1           <i>; načtení stavu všech osmi tlačítek do záchytného registru</i>
        lda #$00
        sta JOYPAD1           <i>; začátek načítání jednotlivých bitů se stavy tlačítek v tomto pořadí:</i>
                              <i>; </i>
                              <i>; 1) A                      </i>
                              <i>; 2) B                      </i>
                              <i>; 3) Select                 </i>
                              <i>; 4) Start                  </i>
                              <i>; 5) Up                     </i>
                              <i>; 6) Down                   </i>
                              <i>; 7) Left                   </i>
                              <i>; 8) Right</i>

        read_button           <i>; detekce stisku tlačítka A</i>
        read_button           <i>; detekce stisku tlačítka B</i>
        read_button           <i>; detekce stisku tlačítka Select</i>
        read_button           <i>; detekce stisku tlačítka Start</i>

        read_button           <i>; detekce stisku tlačítka Up</i>
        beq up_not_pressed    <i>; není stisknuto? => skok</i>
        inc YSCROLL           <i>; zvýšení offsetu</i>

up_not_pressed:
        read_button           <i>; detekce stisku tlačítka Down</i>
        beq down_not_pressed  <i>; není stisknuto? => skok</i>
        dec YSCROLL           <i>; snížení offsetu</i>

down_not_pressed:
        read_button           <i>; detekce stisku tlačítka Left</i>
        beq left_not_pressed  <i>; není stisknuto? => skok</i>
        inc XSCROLL           <i>; zvýšení offsetu</i>

left_not_pressed:
        read_button           <i>; detekce stisku tlačítka Right</i>
        beq right_not_pressed <i>; není stisknuto? => skok</i>
        dec XSCROLL           <i>; snížení offsetu</i>

right_not_pressed:

        lda XSCROLL           <i>; nastavení scrollingu</i>
        sta SCROLL
        lda YSCROLL
        sta SCROLL

        lda #$02              <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti                   <i>; návrat z přerušení</i>
.endproc
</pre>



<p><a name="k16"></a></p>
<h2 id="k16">16. Úplný zdrojový kód třetího demonstračního příkladu</h2>

<p>Úplný zdrojový kód dnešního posledního demonstračního příkladu
(v&nbsp;pořadí již třicátého příkladu pro NES) je dostupný na adrese <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example30.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example30.asm</a>.
Pro překlad a slinkování tohoto příkladu je zapotřebí i <a
href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/Makefile">Makefile</a>
a příkaz <strong>make example30.nes</strong>:</p>

<pre>
<i>; ---------------------------------------------------------------------</i>
<i>; Kostra programu pro herní konzoli NES</i>
<i>; Nastavení barvové palety, zvýšení intenzity barvy</i>
<i>; Setup PPU přes makro</i>
<i>; Definice pozadí a zobrazení pozadí.</i>
<i>; Scrolling pozadí s využitím ovladače.</i>
<i>;</i>
<i>; Založeno na příkladu https://github.com/depp/ctnes/tree/master/nesdev/01</i>
<i>; Taktéž založeno na https://nerdy-nights.nes.science/#main_tutorial-3</i>
<i>; Viz též článek na https://www.moria.us/blog/2018/03/nes-development</i>
<i>; Audio https://raw.githubusercontent.com/iliak/nes/master/doc/apu_ref.txt</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Jména řídicích registrů použitých v kódu</i>
PPUCTRL         = $2000
PPUMASK         = $2001
PPUSTATUS       = $2002
SCROLL          = $2005
PPUADDR         = $2006
PPUDATA         = $2007
DMC_FREQ        = $4010
OAM_DMA         = $4014
&nbsp;
<i>; Další důležité adresy</i>
PALETTE         = $3f00
NAME_TABLE_0    = $2000
ATTRIB_TABLE_0  = $23c0
&nbsp;
<i>; Ovladače</i>
JOYPAD1         = $4016
JOYPAD2         = $4017
&nbsp;
<i>; Adresy globálních proměnných</i>
XSCROLL         = $0203       <i>; adresa buňky paměti pro scrolling ve směru x-ové osy</i>
YSCROLL         = $0200       <i>; adresa buňky paměti pro scrolling ve směru y-ové osy</i>
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice maker</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.macro <strong>setup_cpu</strong>
        <i>; nastavení stavu CPU</i>
        sei                     <i>; zákaz přerušení</i>
        cld                     <i>; vypnutí dekadického režimu (není podporován)</i>
&nbsp;
        ldx #$ff
        txs                     <i>; vrchol zásobníku nastaven na 0xff (první stránka)</i>
.endmacro
&nbsp;
.macro <strong>wait_for_frame</strong>
:       bit PPUSTATUS            <i>; test obsahu registru PPUSTATUS </i>
        bpl :-                   <i>; skok, pokud je příznak N nulový</i>
.endmacro
&nbsp;
.macro <strong>clear_ram</strong>
        lda #$00                <i>; vynulování registru A</i>
:       sta $000, x             <i>; vynulování X-tého bajtu v nulté stránce</i>
        sta $100, x
        sta $200, x
        sta $300, x
        sta $400, x
        sta $500, x
        sta $600, x
        sta $700, x             <i>; vynulování X-tého bajtu v sedmé stránce</i>
        inx                     <i>; přechod na další bajt</i>
        bne :-                  <i>; po přetečení 0xff -> 0x00 konec smyčky</i>
.endmacro
&nbsp;
.macro <strong>ppu_data_palette_address</strong>
        lda PPUSTATUS   <i>; reset záchytného registru</i>
        lda #&gt;PALETTE   <i>; nastavení adresy pro barvovou paletu $3f00</i>
        sta PPUADDR
        lda #&lt;PALETTE   <i>; nižší bajt adresy</i>
        sta PPUADDR
.endmacro
&nbsp;
.macro <strong>read_button</strong>
        lda JOYPAD1        <i>; stav tlačítka</i>
        and #%00000001     <i>; maskovat všechny bity kromě prvního</i>
.endmacro
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Definice hlavičky obrazu ROM</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Size of PRG in units of 16 KiB.</i>
prg_npage = 2
&nbsp;
<i>; Size of CHR in units of 8 KiB.</i>
chr_npage = 1
&nbsp;
<i>; INES mapper number.</i>
mapper = 0
&nbsp;
<i>; Mirroring (0 = horizontal, 1 = vertical)</i>
mirroring = 0
&nbsp;
.segment "HEADER"
        .byte $4e, $45, $53, $1a
        .byte prg_npage
        .byte chr_npage
        .byte ((mapper &amp; $0f) &lt;&lt; 4) | (mirroring &amp; 1)
        .byte mapper &amp; $f0
&nbsp;
.segment "ZEROPAGE"
.segment "STARTUP"
.segment "CODE"
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Blok paměti s definicí dlaždic 8x8 pixelů</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "CHR0a"
.segment "CHR0b"
&nbsp;
&nbsp;
.code
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Programový kód rutin pro NMI, RESET a IRQ volaných automaticky CPU</i>
<i>;</i>
<i>; viz též https://www.pagetable.com/?p=410</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
<i>; Obslužná rutina pro NMI (nemaskovatelné přerušení, vertical blank)</i>
&nbsp;
.proc <strong>nmi</strong>
        jsr display_scroll_offsets
&nbsp;
        lda #$01
        sta JOYPAD1           <i>; načtení stavu všech osmi tlačítek do záchytného registru</i>
        lda #$00
        sta JOYPAD1           <i>; začátek načítání jednotlivých bitů se stavy tlačítek v tomto pořadí:</i>
                              <i>; </i>
                              <i>; 1) A                      </i>
                              <i>; 2) B                      </i>
                              <i>; 3) Select                 </i>
                              <i>; 4) Start                  </i>
                              <i>; 5) Up                     </i>
                              <i>; 6) Down                   </i>
                              <i>; 7) Left                   </i>
                              <i>; 8) Right</i>
&nbsp;
        read_button           <i>; detekce stisku tlačítka A</i>
        read_button           <i>; detekce stisku tlačítka B</i>
        read_button           <i>; detekce stisku tlačítka Select</i>
        read_button           <i>; detekce stisku tlačítka Start</i>
&nbsp;
        read_button           <i>; detekce stisku tlačítka Up</i>
        beq up_not_pressed    <i>; není stisknuto? => skok</i>
        inc YSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
up_not_pressed:
        read_button           <i>; detekce stisku tlačítka Down</i>
        beq down_not_pressed  <i>; není stisknuto? => skok</i>
        dec YSCROLL           <i>; snížení offsetu</i>
&nbsp;
down_not_pressed:
        read_button           <i>; detekce stisku tlačítka Left</i>
        beq left_not_pressed  <i>; není stisknuto? => skok</i>
        inc XSCROLL           <i>; zvýšení offsetu</i>
&nbsp;
left_not_pressed:
        read_button           <i>; detekce stisku tlačítka Right</i>
        beq right_not_pressed <i>; není stisknuto? => skok</i>
        dec XSCROLL           <i>; snížení offsetu</i>
&nbsp;
right_not_pressed:
&nbsp;
        lda XSCROLL           <i>; nastavení scrollingu</i>
        sta SCROLL
        lda YSCROLL
        sta SCROLL
&nbsp;
        lda #$02              <i>; horní bajt adresy pro přenos + zahájení přenosu</i>
        sta OAM_DMA
        rti                   <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro IRQ (maskovatelné přerušení)</i>
&nbsp;
.proc <strong>irq</strong>
        rti                   <i>; návrat z přerušení</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; Obslužná rutina pro RESET</i>
&nbsp;
.proc <strong>reset</strong>
        <i>; nastavení stavu CPU</i>
        setup_cpu
&nbsp;
        <i>; nastavení řídicích registrů</i>
        ldx #$40
        stx $4017               <i>; zákaz IRQ z APU</i>
&nbsp;
        ldx #$00
        stx PPUCTRL             <i>; nastavení PPUCTRL = 0 (NMI)</i>
        stx PPUMASK             <i>; nastavení PPUMASK = 0</i>
        stx DMC_FREQ            <i>; zákaz DMC IRQ</i>
&nbsp;
        ldx #$40
        stx $4017               <i>; interrupt inhibit bit</i>
&nbsp;
        <i>; čekání na vnitřní inicializaci PPU (dva snímky)</i>
        wait_for_frame
        wait_for_frame
&nbsp;
        <i>; vymazání obsahu RAM</i>
        clear_ram
&nbsp;
        <i>; čekání na další snímek</i>
        wait_for_frame
&nbsp;
        <i>; nastavení tabulek jmen</i>
        jsr clear_nametables  <i>; zavolání subrutiny </i>
&nbsp;
        <i>; nastavení barvové palety</i>
        jsr load_palette      <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení tabulky jmen</i>
        jsr load_nametable    <i>; zavolání subrutiny</i>
&nbsp;
        <i>; načtení atributů</i>
        jsr load_attributes   <i>; zavolání subrutiny</i>
&nbsp;
        lda #%00001110        <i>; povolení zobrazení pozadí, nikoli ovšem spritů</i>
        sta PPUMASK
&nbsp;
        lda #0                <i>; vynulovat počitadlo spritů</i>
        sta XSCROLL
        sta YSCROLL
&nbsp;
        cli                   <i>; vynulování bitu I - povolení přerušení</i>
        lda #%10010000      
        sta PPUCTRL           <i>; při každém VBLANK se vyvolá NMI (důležité!)</i>
&nbsp;
        <i>; vlastní herní smyčka je prozatím prázdná</i>
game_loop:
        jmp game_loop           <i>; nekonečná smyčka (později rozšíříme)</i>
.endproc
&nbsp;
<i>; zobrazení dvou hexadecimálních cifer na pozadí</i>
.macro <strong>draw_two_digits</strong> address
        lda address           <i>; přečíst offset pro scrolling</i>
        lsr a                 <i>; získání horního nibblu</i>
        lsr a
        lsr a
        lsr a
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
&nbsp;
        lda address           <i>; opět přečíst offset pro scrolling</i>
        and #$0f              <i>; získání horního nibblu</i>
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
.endmacro
&nbsp;
<i>; zobrazení offsetů</i>
.proc <strong>display_scroll_offsets</strong>
        lda PPUSTATUS         <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0+2  <i>; horní bajt adresy $23c0</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0+10 <i>; spodní bajt adresy $23c0</i>
        sta PPUADDR
&nbsp;
        draw_two_digits XSCROLL
        
        lda #$24              <i>; mezera (tvar oblohy)</i>
        sta PPUDATA           <i>; uložení tvaru (cifry) do pozadí</i>
&nbsp;
        draw_two_digits YSCROLL
&nbsp;
        rts
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; vynulování tabulek jmen</i>
.proc <strong>clear_nametables</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
        ldx #$08           <i>; počitadlo stránek (8)</i>
        ldy #$00           <i>; X a Y tvoří 16bitový čítač</i>
        lda #$24           <i>; dlaždice číslo $24 představuje oblohu</i>
:
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        dey                <i>; snížení hodnoty počitadla</i>
        bne :-             <i>; skok dokud se nezaplní celá stránka</i>
        dex                <i>; snížení hodnoty počitadla stránek</i>
        bne :-             <i>; skok dokud se nezaplní 8 stránek</i>
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
<i>; nastavení barvové palety</i>
.proc <strong>load_palette</strong>
        ppu_data_palette_address
&nbsp;
        <i>; $3f00-$3f0f - paleta pozadí</i>
        <i>; $3f10-$3f1f - paleta spritů</i>
&nbsp;
        ldx #$00        <i>; vynulovat počitadlo a offset</i>
&nbsp;
:
        lda palette, x  <i>; načíst bajt s offsetem</i>
        sta PPUDATA     <i>; zápis barvy do PPU</i>
        inx             <i>; zvýšit počitadlo/offset</i>
        cpx #32         <i>; limit počtu barev</i>
        bne :-          <i>; opakovat smyčku 32x</i>
&nbsp;
        rts             <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení tabulky jmen</i>
.proc <strong>load_nametable</strong>
        lda PPUSTATUS      <i>; reset záchytného registru</i>
        lda #&gt;NAME_TABLE_0 <i>; horní bajt adresy</i>
        sta PPUADDR
        lda #&lt;NAME_TABLE_0 <i>; spodní bajt adresy</i>
        sta PPUADDR
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+256,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        ldx #$00           <i>; počitadlo</i>
:
        lda nametabledata+512,X
        sta PPUDATA        <i>; zápis indexu dlaždice</i>
        inx 
        bne :-
&nbsp;
        rts                <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; načtení atributů</i>
.proc load_attributes
        lda PPUSTATUS        <i>; reset záchytného registru</i>
        lda #&gt;ATTRIB_TABLE_0 <i>; horní bajt adresy $23c0</i>
        sta PPUADDR
        lda #&lt;ATTRIB_TABLE_0 <i>; spodní bajt adresy $23c0</i>
        sta PPUADDR
&nbsp;
        ldx #0               <i>; počitadlo 64 bajtů</i>
&nbsp;
:
        lda attributedata,X  <i>; načtení čtyř atributů</i>
        sta PPUDATA          <i>; zápis indexu</i>
        inx                  <i>; snížení hodnoty počitadla</i>
        cpx #64
        bne :-               <i>; opakování smyčky</i>
&nbsp;
        rts                  <i>; návrat ze subrutiny</i>
.endproc
&nbsp;
&nbsp;
&nbsp;
<i>; samotná barvová paleta</i>
palette:
    .byte $22, $29, $1a, $0F, $22, $36, $17, $0F, $22, $30, $21, $0F, $22, $27, $17, $0F  <i>; barvy pozadí</i>
    .byte $22, $16, $27, $18, $22, $1A, $30, $27, $22, $16, $30, $27, $22, $0F, $36, $17  <i>; barvy spritů</i>
&nbsp;
&nbsp;
<i>; tabulka jmen</i>
nametabledata:
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$D0,$E8,$D1,$D0,$D1,$DE,$D1,$D8,$D0,$D1,$26,$29,$29,$DE,$D1,$D0,$D1,$D0,$D1,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$42,$42,$DB,$42,$DB,$42,$DB,$DB,$42,$26,$29,$29,$DB,$42,$DB,$42,$DB,$42,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$DB,$DE,$DF,$DB,$DB,$DB,$26,$29,$29,$DE,$DF,$DB,$DB,$E4,$E5,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DE,$43,$DB,$42,$DB,$DB,$DB,$26,$29,$29,$DB,$42,$DB,$DB,$E6,$E3,$26,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$DB,$DB,$DB,$DB,$42,$DB,$DB,$DB,$D4,$D9,$26,$29,$29,$DB,$DB,$D4,$D9,$D4,$D9,$E7,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$95,$95,$95,$95,$95,$95,$95,$95,$97,$98,$78,$78,$78,$95,$95,$97,$98,$97,$98,$95,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
&nbsp;
    .byte $24,$1b,$18,$18,$1d,$24,$25,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24
    .byte $24,$24,$a5,$a6,$24,$24,$24,$24,$24,$24,$24,$24,$53,$54,$24,$24,$24,$a7,$a8,$24,$45,$45,$24,$24,$45,$45,$45,$45,$53,$54,$24,$24
    .byte $24,$24,$a7,$a8,$47,$47,$24,$24,$47,$47,$47,$47,$55,$56,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$24,$55,$56,$24,$24
&nbsp;
<i>; tabulka atributů</i>
attributedata:
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111 <i>; žluto-hnědá paleta</i>
    .byte %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111, %11111111
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; zelená paleta</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
    .byte %10101010, %10101010, %10101010, %10111111, %11111111, %10101010, %11111111, %10101010
    .byte %01010101, %10101010, %01010101, %11101111, %11111111, %01010101, %11111111, %01010101
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000 <i>; jen obloha - průhledné pixely</i>
    .byte %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000, %00000000
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Tabulka vektorů CPU</i>
<i>; ---------------------------------------------------------------------</i>
&nbsp;
.segment "VECTORS"
.addr nmi
.addr reset
.addr irq
&nbsp;
&nbsp;
&nbsp;
.segment "CHARS"
    .incbin "mario.chr"
&nbsp;
&nbsp;
&nbsp;
<i>; ---------------------------------------------------------------------</i>
<i>; Finito</i>
<i>; ---------------------------------------------------------------------</i>
</pre>



<p><a name="k17"></a></p>
<h2 id="k17">17. Ukázky posunutého pozadí se zobrazením offsetů</h2>

<p></p>



<p><a name="k18"></a></p>
<h2 id="k18">18. Od assembleru k&nbsp;jazyku C</h2>

<p></p>



<p><a name="k19"></a></p>
<h2 id="k19">19. Repositář s&nbsp;demonstračními příklady</h2>

<p>Demonstrační příklady napsané v&nbsp;assembleru, které jsou určené pro
překlad pomocí assembleru <strong>ca65</strong> (jenž je součástí
<strong>cc65</strong>), byly uložen do Git repositáře, který je dostupný na
adrese <a
href="https://github.com/tisnik/8bit-fame">https://github.com/tisnik/8bit-fame</a>.
Jednotlivé demonstrační příklady si můžete v&nbsp;případě potřeby stáhnout i
jednotlivě bez nutnosti klonovat celý (dnes již poměrně rozsáhlý)
repositář:</p>

<table>
<tr><th> #</th><th>Příklad</th><th>Stručný popis</th><th>Adresa</th></tr>
<tr><td> 1</td><td>example01.asm</td><td>zdrojový kód příkladu tvořeného kostrou aplikace pro NES</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example01.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example01.asm</a></td></tr>
<tr><td> 2</td><td>example02.asm</td><td>použití standardní konfigurace linkeru pro konzoli NES</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example02.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example02.asm</a></td></tr>
<tr><td> 3</td><td>example03.asm</td><td>symbolická jména řídicích registrů PPU</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example03.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example03.asm</a></td></tr>
<tr><td> 4</td><td>example04.asm</td><td>zjednodušený zápis lokálních smyček v&nbsp;assembleru</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example04.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example04.asm</a></td></tr>
<tr><td> 5</td><td>example05.asm</td><td>zvukový výstup s&nbsp;využitím prvního &bdquo;square&ldquo; kanálu</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example05.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example05.asm</a></td></tr>
<tr><td> 6</td><td>example06.asm</td><td>použití maker bez parametrů</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example06.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example06.asm</a></td></tr>
<tr><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td></tr>
<tr><td> 7</td><td>example07.asm</td><td>nastavení barvové palety, zvýšení intenzity zvolené barvové složky</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example07.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example07.asm</a></td></tr>
<tr><td> 8</td><td>example08.asm</td><td>využití operátorů &lt; a &gt;</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example08.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example08.asm</a></td></tr>
<tr><td> 9</td><td>example09.asm</td><td>vymazání barvové palety realizované makrem</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example09.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example09.asm</a></td></tr>
<tr><td>10</td><td>example10.asm</td><td>vymazání barvové palety realizované podprogramem</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example10.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example10.asm</a></td></tr>
<tr><td>11</td><td>example11.asm</td><td>nastavení barvové palety pozadí i spritů</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example11.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example11.asm</a></td></tr>
<tr><td>12</td><td>example12.asm</td><td>refaktoring předchozího příkladu makrem</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example12.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example12.asm</a></td></tr>
<tr><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td></tr>
<tr><td>13</td><td>example13.asm</td><td>zobrazení spritů tvořících Maria</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example13.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example13.asm</a></td></tr>
<tr><td>14</td><td>example14.asm</td><td>posun spritů, aby se zdůraznila jejich nezávislost</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example14.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example14.asm</a></td></tr>
<tr><td>15</td><td>example15.asm</td><td>větší množství spritů na obrazovce rozdělených do řádků</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example15.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example15.asm</a></td></tr>
<tr><td>16</td><td>example16.asm</td><td>větší množství spritů na obrazovce na jediném řádku</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example16.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example16.asm</a></td></tr>
<tr><td>17</td><td>example17.asm</td><td>pohyb jednoho spritu pomocí ovladače</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example17.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example17.asm</a></td></tr>
<tr><td>18</td><td>example18.asm</td><td>odvozeno z&nbsp;předchozího příkladu, symbolická jména adres</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example18.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example18.asm</a></td></tr>
<tr><td>19</td><td>example19.asm</td><td>odvozeno z&nbsp;předchozího příkladu, pomocná makra pro pohyb spritu</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example19.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example19.asm</a></td></tr>
<tr><td>20</td><td>example20.asm</td><td>pohyb spritu je založen na instrukcích <strong>INC</strong> a <strong>DEC</strong></td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example20.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example20.asm</a></td></tr>
<tr><td>21</td><td>example21.asm</td><td>přesun celého Maria (8 spritů)</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example21.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example21.asm</a></td></tr>
<tr><td>22</td><td>example22.asm</td><td>(rychlá) změna barvové palety spritů tlačítkem A</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example22.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example22.asm</a></td></tr>
<tr><td>23</td><td>example23.asm</td><td>realizace čítače pro snížení frekvence změn barvové palety</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example23.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example23.asm</a></td></tr>
<tr><td>24</td><td>example24.asm</td><td>horizontální a vertikální zrcadlení spritů řízené hráčem stiskem tlačítek</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example24.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example24.asm</a></td></tr>
<tr><td>25</td><td>example25.asm</td><td>definice pozadí a zobrazení pozadí</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example25.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example25.asm</a></td></tr>
<tr><td>26</td><td>example26.asm</td><td>zobrazení pozadí i spritů</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example26.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example26.asm</a></td></tr>
<tr><td>27</td><td>example27.asm</td><td>pohyb spritu řízený ovladačem, změna atributů spritů</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example27.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example27.asm</a></td></tr>
<tr><td>28</td><td>example28.asm</td><td>definice pozadí přes téměř celou obrazovku</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example28.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example28.asm</a></td></tr>
<tr><td>29</td><td>example29.asm</td><td>scrolling pozadí s&nbsp;využitím ovladače</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example29.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example29.asm</a></td></tr>
<tr><td>30</td><td>example30.asm</td><td>zobrazení hodnot offsetů pro pozadí</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example30.asm">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/example30.asm</a></td></tr>
<tr><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td><td>&nbsp;</td></tr>
<tr><td>31</td><td>link.cfg</td><td>konfigurace segmentů pro linker <strong>ld65</strong></td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/link.cfg">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/link.cfg</a></td></tr>
<tr><td>32</td><td>Makefile</td><td>Makefile pro překlad a slinkování všech příkladů</td><td><a href="https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/Makefile">https://github.com/tisnik/8bit-fame/blob/master/NES-ca65/Makefile</a></td></tr>
</table>

<p><div class="rs-tip-major">Poznámka: pro slinkování a spuštění dnešních
demonstračních příkladů potřebujete i soubor <a
href="https://github.com/ericdigioia/6502-Assembly-NES-Game-Development-Playground/raw/main/mario.chr">mario.chr</a>.
Ten je stažen automaticky po zadání <strong>make example16</strong> až
<strong>make example23</strong>.</div></p>



<p><a name="k20"></a></p>
<h2 id="k20">20. Odkazy na Internetu</h2>

<ol>

<li>The Thirty Million Line Problem<br />
<a href="https://www.youtube.com/watch?v=kZRE7HIO3vk">https://www.youtube.com/watch?v=kZRE7HIO3vk</a>
</li>

<li>NesDev.org<br />
<a href="https://www.nesdev.org/">https://www.nesdev.org/</a>
</li>

<li>The Sprite Attribute Byte<br />
<a href="https://www.patater.com/nes-asm-tutorials/day-17/">https://www.patater.com/nes-asm-tutorials/day-17/</a>
</li>

<li>How to Program an NES game in C<br />
<a href="https://nesdoug.com/">https://nesdoug.com/</a>
</li>

<li>Cycle reference chart<br />
<a href="https://www.nesdev.org/wiki/Cycle_reference_chart">https://www.nesdev.org/wiki/Cycle_reference_chart</a>
</li>

<li>Getting Started Programming in C: Coding a Retro Game with C Part 2<br />
<a href="https://retrogamecoders.com/getting-started-with-c-cc65/">https://retrogamecoders.com/getting-started-with-c-cc65/</a>
</li>

<li>NES game development in 6502 assembly - Part 1<br />
<a href="https://kibrit.tech/en/blog/nes-game-development-part-1">https://kibrit.tech/en/blog/nes-game-development-part-1</a>
</li>

<li>NES (Nintendo Entertainment System) controller pinout<br />
<a href="https://pinoutguide.com/Game/NES_controller_pinout.shtml">https://pinoutguide.com/Game/NES_controller_pinout.shtml</a>
</li>

<li>NES Controller Shift Register<br />
<a href="https://www.allaboutcircuits.com/uploads/articles/nes-controller-arduino.png?v=1469416980041">https://www.allaboutcircuits.com/uploads/articles/nes-controller-arduino.png?v=1469416980041</a>
</li>

<li>"Game Development in Eight Bits" by Kevin Zurawel<br />
<a href="https://www.youtube.com/watch?v=TPbroUDHG0s&amp;list=PLcGKfGEEONaBjSfQaSiU9yQsjPxxDQyV8&amp;index=4">https://www.youtube.com/watch?v=TPbroUDHG0s&amp;list=PLcGKfGEEONaBjSfQaSiU9yQsjPxxDQyV8&amp;index=4</a>
</li>

<li>Game Development for the 8-bit NES: A class by Bob Rost<br />
<a href="http://bobrost.com/nes/">http://bobrost.com/nes/</a>
</li>

<li>Game Development for the 8-bit NES: Lecture Notes<br />
<a href="http://bobrost.com/nes/lectures.php">http://bobrost.com/nes/lectures.php</a>
</li>

<li>NES Graphics Explained<br />
<a href="https://www.youtube.com/watch?v=7Co_8dC2zb8">https://www.youtube.com/watch?v=7Co_8dC2zb8</a>
</li>

<li>NES GAME PROGRAMMING PART 1<br />
<a href="https://rpgmaker.net/tutorials/227/?post=240020">https://rpgmaker.net/tutorials/227/?post=240020</a>
</li>

<li>NES 6502 Programming Tutorial - Part 1: Getting Started<br />
<a href="https://dev.xenforo.relay.cool/index.php?threads/nes-6502-programming-tutorial-part-1-getting-started.858389/">https://dev.xenforo.relay.cool/index.php?threads/nes-6502-programming-tutorial-part-1-getting-started.858389/</a>
</li>

<li>Minimal NES example using ca65<br />
<a href="https://github.com/bbbradsmith/NES-ca65-example">https://github.com/bbbradsmith/NES-ca65-example</a>
</li>

<li>List of 6502-based Computers and Consoles<br />
<a href="https://www.retrocompute.co.uk/list-of-6502-based-computers-and-consoles/">https://www.retrocompute.co.uk/list-of-6502-based-computers-and-consoles/</a>
</li>

<li>History of video game consoles (second generation): Wikipedia<br />
<a href="http://en.wikipedia.org/wiki/History_of_video_game_consoles_(second_generation)">http://en.wikipedia.org/wiki/History_of_video_game_consoles_(second_generation)</a>
</li>

<li>6502 - the first RISC &micro;P<br />
<a href="http://ericclever.com/6500/">http://ericclever.com/6500/</a>
</li>

<li>3 Generations of Game Machine Architecture<br />
<a href="http://www.atariarchives.org/dev/CGEXPO99.html">http://www.atariarchives.org/dev/CGEXPO99.html</a>
</li>

<li>bee - The Multi-Console Emulator<br />
<a href="http://www.thebeehive.ws/">http://www.thebeehive.ws/</a>
</li>

<li>Nerdy Nights Mirror<br />
<a href="https://nerdy-nights.nes.science/">https://nerdy-nights.nes.science/</a>
</li>

<li>The Nerdy Nights ca65 Remix<br />
<a href="https://github.com/ddribin/nerdy-nights">https://github.com/ddribin/nerdy-nights</a>
</li>

<li>NES Development Day 1: Creating a ROM<br />
<a href="https://www.moria.us/blog/2018/03/nes-development">https://www.moria.us/blog/2018/03/nes-development</a>
</li>

<li>How to Start Making NES Games<br />
<a href="https://www.matthughson.com/2021/11/17/how-to-start-making-nes-games/">https://www.matthughson.com/2021/11/17/how-to-start-making-nes-games/</a>
</li>

<li>ca65 Users Guide<br />
<a href="https://cc65.github.io/doc/ca65.html">https://cc65.github.io/doc/ca65.html</a>
</li>

<li>cc65 Users Guide<br />
<a href="https://cc65.github.io/doc/cc65.html">https://cc65.github.io/doc/cc65.html</a>
</li>

<li>ld65 Users Guide<br />
<a href="https://cc65.github.io/doc/ld65.html">https://cc65.github.io/doc/ld65.html</a>
</li>

<li>da65 Users Guide<br />
<a href="https://cc65.github.io/doc/da65.html">https://cc65.github.io/doc/da65.html</a>
</li>

<li>Nocash NES Specs<br />
<a href="http://nocash.emubase.de/everynes.htm">http://nocash.emubase.de/everynes.htm</a>
</li>

<li>Nintendo Entertainment System<br />
<a href="http://cs.wikipedia.org/wiki/NES">http://cs.wikipedia.org/wiki/NES</a>
</li>

<li>Nintendo Entertainment System Architecture<br />
<a href="http://nesdev.icequake.net/nes.txt">http://nesdev.icequake.net/nes.txt</a>
</li>

<li>NesDev<br />
<a href="http://nesdev.parodius.com/">http://nesdev.parodius.com/</a>
</li>

<li>2A03 technical reference<br />
<a href="http://nesdev.parodius.com/2A03%20technical%20reference.txt">http://nesdev.parodius.com/2A03%20technical%20reference.txt</a>
</li>

<li>NES Dev wiki: 2A03<br />
<a href="http://wiki.nesdev.com/w/index.php/2A03">http://wiki.nesdev.com/w/index.php/2A03</a>
</li>

<li>Ricoh 2A03<br />
<a href="http://en.wikipedia.org/wiki/Ricoh_2A03">http://en.wikipedia.org/wiki/Ricoh_2A03</a>
</li>

<li>2A03 pinouts<br />
<a href="http://nesdev.parodius.com/2A03_pinout.txt">http://nesdev.parodius.com/2A03_pinout.txt</a>
</li>

<li>27c3: Reverse Engineering the MOS 6502 CPU (en)<br />
<a href="https://www.youtube.com/watch?v=fWqBmmPQP40">https://www.youtube.com/watch?v=fWqBmmPQP40</a>
</li>

<li>“Hello, world” from scratch on a 6502 — Part 1<br />
<a href="https://www.youtube.com/watch?v=LnzuMJLZRdU">https://www.youtube.com/watch?v=LnzuMJLZRdU</a>
</li>

<li>A Tour of 6502 Cross-Assemblers<br />
<a href="https://bumbershootsoft.wordpress.com/2016/01/31/a-tour-of-6502-cross-assemblers/">https://bumbershootsoft.wordpress.com/2016/01/31/a-tour-of-6502-cross-assemblers/</a>
</li>

<li>Nintendo Entertainment System (NES)<br />
<a href="https://8bitworkshop.com/docs/platforms/nes/">https://8bitworkshop.com/docs/platforms/nes/</a>
</li>

<li>Question about NES vectors and PPU<br />
<a href="https://archive.nes.science/nesdev-forums/f10/t4154.xhtml">https://archive.nes.science/nesdev-forums/f10/t4154.xhtml</a>
</li>

<li>How do mapper chips actually work?<br />
<a href="https://archive.nes.science/nesdev-forums/f9/t13125.xhtml">https://archive.nes.science/nesdev-forums/f9/t13125.xhtml</a>
</li>

<li>INES<br />
<a href="https://www.nesdev.org/wiki/INES">https://www.nesdev.org/wiki/INES</a>
</li>

<li>NES Basics and Our First Game<br />
<a href="http://thevirtualmountain.com/nes/2017/03/08/nes-basics-and-our-first-game.html">http://thevirtualmountain.com/nes/2017/03/08/nes-basics-and-our-first-game.html</a>
</li>

<li>Where is the reset vector in a .nes file?<br />
<a href="https://archive.nes.science/nesdev-forums/f10/t17413.xhtml">https://archive.nes.science/nesdev-forums/f10/t17413.xhtml</a>
</li>

<li>CPU memory map<br />
<a href="https://www.nesdev.org/wiki/CPU_memory_map">https://www.nesdev.org/wiki/CPU_memory_map</a>
</li>

<li>How to make NES music<br />
<a href="http://blog.snugsound.com/2008/08/how-to-make-nes-music.html">http://blog.snugsound.com/2008/08/how-to-make-nes-music.html</a>
</li>

<li>Nintendo Entertainment System Architecture<br />
<a href="http://nesdev.icequake.net/nes.txt">http://nesdev.icequake.net/nes.txt</a>
</li>

<li>MIDINES<br />
<a href="http://www.wayfar.net/0xf00000_overview.php">http://www.wayfar.net/0xf00000_overview.php</a>
</li>

<li>FamiTracker<br />
<a href="http://famitracker.com/">http://famitracker.com/</a>
</li>

<li>nerdTracker II<br />
<a href="http://nesdev.parodius.com/nt2/">http://nesdev.parodius.com/nt2/</a>
</li>

<li>How NES Graphics work<br />
<a href="http://nesdev.parodius.com/nesgfx.txt">http://nesdev.parodius.com/nesgfx.txt</a>
</li>

<li>NES Technical/Emulation/Development FAQ<br />
<a href="http://nesdev.parodius.com/NESTechFAQ.htm">http://nesdev.parodius.com/NESTechFAQ.htm</a>
</li>

<li>Adventures with ca65<br />
<a href="https://atariage.com/forums/topic/312451-adventures-with-ca65/">https://atariage.com/forums/topic/312451-adventures-with-ca65/</a>
</li>

<li>example ca65 startup code<br />
<a href="https://atariage.com/forums/topic/209776-example-ca65-startup-code/">https://atariage.com/forums/topic/209776-example-ca65-startup-code/</a>
</li>

<li>6502 PRIMER: Building your own 6502 computer<br />
<a href="http://wilsonminesco.com/6502primer/">http://wilsonminesco.com/6502primer/</a>
</li>

<li>6502 Instruction Set<br />
<a href="https://www.masswerk.at/6502/6502_instruction_set.html">https://www.masswerk.at/6502/6502_instruction_set.html</a>
</li>

<li>Chip Hall of Fame: MOS Technology 6502 Microprocessor<br />
<a href="https://spectrum.ieee.org/tech-history/silicon-revolution/chip-hall-of-fame-mos-technology-6502-microprocessor">https://spectrum.ieee.org/tech-history/silicon-revolution/chip-hall-of-fame-mos-technology-6502-microprocessor</a>
</li>

<li>Single-board computer<br />
<a href="https://en.wikipedia.org/wiki/Single-board_computer">https://en.wikipedia.org/wiki/Single-board_computer</a>
</li>

<li>www.6502.org<br />
<a href="http://www.6502­.org/">http://www.6502­.org/</a>
</li>

<li>6502 PRIMER: Building your own 6502 computer &ndash; clock generator<br />
<a href="http://wilsonminesco.com/6502primer/ClkGen.html">http://wilsonminesco.com/6502primer/ClkGen.html</a>
</li>

<li>Great Microprocessors of the Past and Present (V 13.4.0)<br />
<a href="http://www.cpushack.com/CPU/cpu.html">http://www.cpushack.com/CPU/cpu.html</a>
</li>

<li>Jak se zrodil procesor?<br />
<a href="https://www.root.cz/clanky/jak-se-zrodil-procesor/">https://www.root.cz/clanky/jak-se-zrodil-procesor/</a>
</li>

<li>Osmibitové mikroprocesory a mikrořadiče firmy Motorola (1)<br />
<a href="https://www.root.cz/clanky/osmibitove-mikroprocesory-a-mikroradice-firmy-motorola-1/">https://www.root.cz/clanky/osmibitove-mikroprocesory-a-mikroradice-firmy-motorola-1/</a>
</li>

<li>Mikrořadiče a jejich použití v jednoduchých mikropočítačích<br />
<a href="https://www.root.cz/clanky/mikroradice-a-jejich-pouziti-v-jednoduchych-mikropocitacich/">https://www.root.cz/clanky/mikroradice-a-jejich-pouziti-v-jednoduchych-mikropocitacich/</a>
</li>

<li>Mikrořadiče a jejich aplikace v jednoduchých mikropočítačích (2)<br />
<a href="https://www.root.cz/clanky/mikroradice-a-jejich-aplikace-v-jednoduchych-mikropocitacich-2/">https://www.root.cz/clanky/mikroradice-a-jejich-aplikace-v-jednoduchych-mikropocitacich-2/</a>
</li>

<li>25 Microchips That Shook the World<br />
<a href="https://spectrum.ieee.org/tech-history/silicon-revolution/25-microchips-that-shook-the-world">https://spectrum.ieee.org/tech-history/silicon-revolution/25-microchips-that-shook-the-world</a>
</li>

<li>Comparison of instruction set architectures<br />
<a href="https://en.wikipedia.org/wiki/Comparison_of_instruction_set_architectures">https://en.wikipedia.org/wiki/Comparison_of_instruction_set_architectures</a>
</li>

<li>Day 1 - Beginning NES Assembly<br />
<a href="https://www.patater.com/nes-asm-tutorials/day-1/">https://www.patater.com/nes-asm-tutorials/day-1/</a>
</li>

<li>Day 2 - A Source Code File's Structure<br />
<a href="https://www.patater.com/nes-asm-tutorials/day-2/">https://www.patater.com/nes-asm-tutorials/day-2/</a>
</li>

<li>Assembly Language Misconceptions<br />
<a href="https://www.youtube.com/watch?v=8_0tbkbSGRE">https://www.youtube.com/watch?v=8_0tbkbSGRE</a>
</li>

<li>How Machine Language Works<br />
<a href="https://www.youtube.com/watch?v=HWpi9n2H3kE">https://www.youtube.com/watch?v=HWpi9n2H3kE</a>
</li>

<li>Super Mario Bros. (1985) Full Walkthrough NES Gameplay [Nostalgia]<br />
<a href="https://www.youtube.com/watch?v=rLl9XBg7wSs">https://www.youtube.com/watch?v=rLl9XBg7wSs</a>
</li>

<li>[Longplay] Castlevania (NES) - All Secrets, No Deaths<br />
<a href="https://www.youtube.com/watch?v=mOTUVXrAOE8">https://www.youtube.com/watch?v=mOTUVXrAOE8</a>
</li>

<li>Herní série Castlevania<br />
<a href="https://www.root.cz/clanky/historie-vyvoje-pocitacovych-her-24-cast-hry-pro-konzoli-nes/#k07">https://www.root.cz/clanky/historie-vyvoje-pocitacovych-her-24-cast-hry-pro-konzoli-nes/#k07</a>
</li>

</ol>



<p></p><p></p>
<p><small>Autor: <a href="http://www.fit.vutbr.cz/~tisnovpa">Pavel Tišnovský</a> &nbsp; 2022</small></p>
</body>
</html>
